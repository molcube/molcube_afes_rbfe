parameters file prep/forcefield.prm

structure file psf prep/system.psf

stream prep/sysinfo.str

selection define lig and segid HYB resid 1
selection define site1sub1 and selection lig atomnames C16A1 H11A1 H12A1 H13A1
selection define site1sub2 and selection lig atomnames O3A2 H11A2 C16A2 H12A2 H13A2
selection define site1sub3 and selection lig atomnames C16A3 H11A3 H12A3 H13A3 O3A3 C17A3 H14A3 H15A3
selection define site2sub1 and selection lig atomnames H11B1
selection define site2sub2 and selection lig atomnames C16B2 H11B2 H12B2 H13B2
selection define site2sub3 and selection lig atomnames O3B3 C16B3 H11B3 H12B3 H13B3
selection define site2sub4 and selection lig atomnames BR1B4
selection define site3sub1 and selection lig atomnames H11C1
selection define site3sub2 and selection lig atomnames CL1C2
selection define site3sub3 and selection lig atomnames N5C3 C16C3 H11C3 O3C3 C17C3 H12C3 H13C3 H14C3
selection define site3sub4 and selection lig atomnames S1C4 O3C4 O4C4 C16C4 H11C4 H12C4 H13C4
selection define site4sub1 and selection lig atomnames H11D1
selection define site4sub2 and selection lig atomnames CL1D2
selection define site4sub3 and selection lig atomnames O3D3 C16D3 H11D3 H12D3 H13D3
selection define site4sub4 and selection lig atomnames BR1D4
selection print

variables set temp 300.0

coordinates velocity {temp}

msld nblocks {nblocks}

variables set prevblock 0
variables set ii 1
! ii -> site 
while <= {ii} {nsites}
   variables set jj 1
   ! jj -> substituent 
   while <= {jj} {nsubs{ii}}
      variables calculate jp0 int + {jj} {prevblock}

      msld call {jp0} site{ii}sub{jj}

      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile

!               B S   t0  tv  tm  fb   q
   msld initialize 0 0 0 0 5 0 0

variables set blockassign 0
variables set prevblock 0
variables set ii 1
while <= {ii} {nsites}
   variables set jj 1
   variables set theta0 1
   while <= {jj} {nsubs{ii}}
      variables calculate jp0 int + {jj} {prevblock}

!               B S   t0  tv  tm  fb   q
      msld initialize {jp0} {ii} {theta0} 0 5 {lams{ii}s{jj}} 0

      ! variables set theta0 -1 ! start all equal
      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile
   msld removescaling bond urey angle  impr 

   msld softcore on
   msld softcore14 off
   ! pmel ex ! msld ewaldtype 2 ! default

variables set prevblock 0
variables set ii 1
while <= {ii} {nsites}
   variables set jj 1
   while <= {jj} {nsubs{ii}}     ! jj and kk specify the substituents 
      variables calculate jp0 int + {jj} {prevblock}
      variables calculate kk int + {jj} 1
      while <= {kk} {nsubs{ii}}
         variables calculate kp0 int + {kk} {prevblock}
         ! put bias potentials here (see manual https://github.com/RyanLeeHayes/BLaDE/blob/master/doc/MAIN.txt )
         ! msld bias lambda1 lambda2 type const coef power 
         !  
         msld bias {jp0} {kp0} 6 0.0 {cs{ii}s{jj}s{ii}s{kk}} 0
         msld bias {jp0} {kp0} 10 -5.56 {xs{ii}s{jj}s{ii}s{kk}} 0
         msld bias {jp0} {kp0} 8 0.017 {ss{ii}s{jj}s{ii}s{kk}} 0
         msld bias {kp0} {jp0} 10 -5.56 {xs{ii}s{kk}s{ii}s{jj}} 0
         msld bias {kp0} {jp0} 8 0.017 {ss{ii}s{kk}s{ii}s{jj}} 0
         variables calculate kk int + {kk} 1
      endwhile
      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile
