parameters file prep/forcefield.prm

structure file psf prep/system.psf

stream prep/sysinfo.str

selection define lig and segid HYB resid 1
selection define site1sub1 and selection lig atomnames CL1A1 C14A1 C15A1 C16A1 H16A1 C17A1 C18A1 H17A1 C19A1 H18A1 H19A1
selection define site1sub2 and selection lig atomnames F1A2 C14A2 C15A2 C16A2 H16A2 C17A2 C18A2 C19A2 F2A2 C20A2 H17A2 H18A2 H19A2 H20A2
selection define site1sub3 and selection lig atomnames F1A3 C14A3 C15A3 C16A3 H16A3 C17A3 C18A3 O3A3 F2A3 C19A3 H17A3 H18A3
selection define site1sub4 and selection lig atomnames C14A4 C15A4 H16A4 C16A4 H17A4 C17A4 H18A4 C18A4 C19A4 O3A4 H19A4 H20A4
selection print

variables set temp 300.0

coordinates velocity {temp}

msld nblocks {nblocks}

variables set prevblock 0
variables set ii 1
! ii -> site 
while <= {ii} {nsites}
   variables set jj 1
   ! jj -> substituent 
   while <= {jj} {nsubs{ii}}
      variables calculate jp0 int + {jj} {prevblock}

      msld call {jp0} site{ii}sub{jj}

      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile

!               B S   t0  tv  tm  fb   q
   msld initialize 0 0 0 0 5 0 0

variables set blockassign 0
variables set prevblock 0
variables set ii 1
while <= {ii} {nsites}
   variables set jj 1
   variables set theta0 1
   while <= {jj} {nsubs{ii}}
      variables calculate jp0 int + {jj} {prevblock}

!               B S   t0  tv  tm  fb   q
      msld initialize {jp0} {ii} {theta0} 0 5 {lams{ii}s{jj}} 0

      ! variables set theta0 -1 ! start all equal
      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile
   msld removescaling bond urey angle  impr 

   msld softcore on
   msld softcore14 off
   ! pmel ex ! msld ewaldtype 2 ! default

variables set prevblock 0
variables set ii 1
while <= {ii} {nsites}
   variables set jj 1
   while <= {jj} {nsubs{ii}}     ! jj and kk specify the substituents 
      variables calculate jp0 int + {jj} {prevblock}
      variables calculate kk int + {jj} 1
      while <= {kk} {nsubs{ii}}
         variables calculate kp0 int + {kk} {prevblock}
         ! put bias potentials here (see manual https://github.com/RyanLeeHayes/BLaDE/blob/master/doc/MAIN.txt )
         ! msld bias lambda1 lambda2 type const coef power 
         !  
         msld bias {jp0} {kp0} 6 0.0 {cs{ii}s{jj}s{ii}s{kk}} 0
         msld bias {jp0} {kp0} 10 -5.56 {xs{ii}s{jj}s{ii}s{kk}} 0
         msld bias {jp0} {kp0} 8 0.017 {ss{ii}s{jj}s{ii}s{kk}} 0
         msld bias {kp0} {jp0} 10 -5.56 {xs{ii}s{kk}s{ii}s{jj}} 0
         msld bias {kp0} {jp0} 8 0.017 {ss{ii}s{kk}s{ii}s{jj}} 0
         variables calculate kk int + {kk} 1
      endwhile
      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile
