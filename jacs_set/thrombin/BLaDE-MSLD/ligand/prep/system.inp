parameters file prep/forcefield.prm

structure file psf prep/system.psf

stream prep/sysinfo.str

selection define lig and segid HYB resid 1
selection define site1sub1 and selection lig atomnames F1A1
selection define site1sub2 and selection lig atomnames CL1A2
selection define site1sub3 and selection lig atomnames BR1A3
selection define site1sub4 and selection lig atomnames I1A4
selection define site1sub5 and selection lig atomnames C22A5 H25A5 H26A5 H27A5
selection define site1sub6 and selection lig atomnames C22A6 H25A6 H26A6 H27A6 C23A6 H28A6 H29A6
selection define site1sub7 and selection lig atomnames H25A7
selection define site2sub1 and selection lig atomnames H25B1
selection define site2sub2 and selection lig atomnames CL1B2
selection define site2sub3 and selection lig atomnames C22B3 H25B3 H26B3 H27B3
selection define site2sub4 and selection lig atomnames F1B4
selection print

variables set temp 300.0

coordinates velocity {temp}

msld nblocks {nblocks}

variables set prevblock 0
variables set ii 1
! ii -> site 
while <= {ii} {nsites}
   variables set jj 1
   ! jj -> substituent 
   while <= {jj} {nsubs{ii}}
      variables calculate jp0 int + {jj} {prevblock}

      msld call {jp0} site{ii}sub{jj}

      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile

!               B S   t0  tv  tm  fb   q
   msld initialize 0 0 0 0 5 0 0

variables set blockassign 0
variables set prevblock 0
variables set ii 1
while <= {ii} {nsites}
   variables set jj 1
   variables set theta0 1
   while <= {jj} {nsubs{ii}}
      variables calculate jp0 int + {jj} {prevblock}

!               B S   t0  tv  tm  fb   q
      msld initialize {jp0} {ii} {theta0} 0 5 {lams{ii}s{jj}} 0

      ! variables set theta0 -1 ! start all equal
      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile
   msld removescaling bond urey angle  impr 

   msld softcore on
   msld softcore14 off
   ! pmel ex ! msld ewaldtype 2 ! default

variables set prevblock 0
variables set ii 1
while <= {ii} {nsites}
   variables set jj 1
   while <= {jj} {nsubs{ii}}     ! jj and kk specify the substituents 
      variables calculate jp0 int + {jj} {prevblock}
      variables calculate kk int + {jj} 1
      while <= {kk} {nsubs{ii}}
         variables calculate kp0 int + {kk} {prevblock}
         ! put bias potentials here (see manual https://github.com/RyanLeeHayes/BLaDE/blob/master/doc/MAIN.txt )
         ! msld bias lambda1 lambda2 type const coef power 
         !  
         msld bias {jp0} {kp0} 6 0.0 {cs{ii}s{jj}s{ii}s{kk}} 0
         msld bias {jp0} {kp0} 10 -5.56 {xs{ii}s{jj}s{ii}s{kk}} 0
         msld bias {jp0} {kp0} 8 0.017 {ss{ii}s{jj}s{ii}s{kk}} 0
         msld bias {kp0} {jp0} 10 -5.56 {xs{ii}s{kk}s{ii}s{jj}} 0
         msld bias {kp0} {jp0} 8 0.017 {ss{ii}s{kk}s{ii}s{jj}} 0
         variables calculate kk int + {kk} 1
      endwhile
      variables calculate jj int + {jj} 1
   endwhile
   variables calculate prevblock int + {prevblock} {nsubs{ii}}
   variables calculate ii int + {ii} 1
endwhile
